plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'edu.sc.seis.launch4j' version '2.4.6'
}

application {
    mainClassName = 'com.carousal.client.MainKt'
}

def jvmModuleArgs = ['--add-exports=javafx.graphics/com.sun.javafx.iio=ALL-UNNAMED',
                     '--add-exports=javafx.graphics/com.sun.javafx.iio.common=ALL-UNNAMED',
                     '--add-exports=javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED',
                     '--add-exports=javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
]

launch4j {
    mainClassName = 'com.carousal.client.MainKt'
    icon = "${projectDir}/resources/carousal256.ico"
    outfile = "Carousal.exe"
    downloadUrl = "https://github.com/AkashWorld/carousal"
    copyright = "GPL3"
    jreMinVersion = "1.11"
    windowTitle = "Carousal"
    jvmOptions = jvmModuleArgs
    bundledJrePath = "./jre11"
    bundledJre64Bit = true
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
    }
}

javafx {
    version = "13"
    modules = ['javafx.controls', 'javafx.base', 'javafx.graphics']
}

group 'akashworld'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation 'io.javalin:javalin:3.6.0'
    implementation 'com.graphql-java:graphql-java:13.0'
    implementation 'org.slf4j:slf4j-simple:1.7.29'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.41"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.10.0"
    implementation 'no.tornado:tornadofx:1.7.19'
    implementation 'com.vdurmont:emoji-java:5.1.1'
    implementation 'de.codecentric.centerdevice:javafxsvg:1.3.0'
    implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-9'
    implementation 'com.offbynull.portmapper:portmapper:2.0.5'
    implementation 'org.imgscalr:imgscalr-lib:4.2'
    implementation 'com.jfoenix:jfoenix:9.0.8'
    implementation "com.google.guava:guava:28.2-jre"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.auth0:java-jwt:3.8.3'
    implementation "com.squareup.okhttp3:okhttp:4.2.1"
    implementation "uk.co.caprica:vlcj:4.2.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    implementation 'me.xdrop:fuzzywuzzy:1.2.0'
    implementation 'org.bitlet:weupnp:0.1.4'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

run {
    applicationDefaultJvmArgs = jvmModuleArgs

    jvmArgs = jvmModuleArgs
}